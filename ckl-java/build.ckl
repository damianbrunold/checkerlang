require Buildsys unqualified;
require IO unqualified;
require OS unqualified;

println(now() + " ckl-java build");
println(now() + " CKL " + checkerlang_version + "-" + checkerlang_platform);

if file_exists("99_temp") then dir_delete("99_temp");
make_dir("99_temp");

make_dir("99_temp/build");
compile_java_files("src/main", "99_temp/build", cp = []);
for file in list_dir("src/main") do  
    if file_extension(file) == ".ckl" then file_copy("src/main/" + file, "99_temp/build/" + file);
end;

make_dir("99_temp/build.test");
compile_java_files("src/test",
                   "99_temp/build.test",
                   cp = join(["lib/junit-platform-console-standalone-1.7.1-all.jar",
                              "99_temp/build"], 
                             FS));
file_copy("src/test/tests.txt", "99_temp/build.test/tests.txt");
run_unittests("build.test",
              "res.test",
              cp = join(["build",
                         "../lib/junit-platform-console-standalone-1.7.1-all.jar"], 
                        FS),
              work_dir = "99_temp",
              report_dir = "99_temp/junit-reports");

if not file_exists("dist") then make_dir("dist");
create_jar_file("dist/ckl-java-repl.jar", "99_temp/build", mainclass="ch.checkerlang.REPL");
create_jar_file("dist/ckl-java-run.jar", "99_temp/build", mainclass="ch.checkerlang.Run");
    
dir_delete("99_temp");

println(now() + " Done");

